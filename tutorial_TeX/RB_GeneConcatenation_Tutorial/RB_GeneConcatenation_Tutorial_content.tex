\section{Concatenating genes to model species evolution}
The simplest model to estimate a species tree when you have several genes is to concatenate your genes and to assume that all gene trees are exactly equal to the species tree.
You thus assume that there is no discordance between gene trees in the hope that the combined information in all genes will provide a reliable signal for the species tree.

\vspace{20mm}

{\begin{framed}
\begin{center}
The exercises assume you have a working installation of RevBayes.
In this introductory tutorial, we will apply the concatenated model to 10 gene alignments from 23 primate species.
We will assume that:
\begin{itemize}
\item The species tree is drawn from a constant birth-death process.
\item All genes share the same tree, that is, both the topology and branch lengths.
\item Each gene has its own set of substitution model and clock parameters which are drawn from a shared prior distribution.
\item Gene sequences are evolved according to an HKY model with gamma distributed rate variation among sites and a strict global clock. 
\item Here, we run an MCMC on this model, using data from 10 genes in 23 mammalian species.
\end{itemize}
Scripts are all placed in {\footnotesize \emph{$tutorials/RB\_GeneConcatenation\_Tutorial/RevBayes\_scripts/$}}. 
\end{center}
\end{framed}}
\vspace{5mm}


\begin{enumerate}
\item Open RevBayes
\item Let's load all 10 gene alignments.
{\tt \begin{snugshade*}
\begin{lstlisting}
# read in each data matrix together which will create a vector of objects
data = readDiscreteCharacterData("data/merged.nex")

# Now we get some useful variables from the data. We need these later on.
num_loci = data.size()
# get the number of species
n_species <- data[1].ntaxa()
# get the taxon information (e.g. the taxon names)
taxa <- data[1].taxa()
n_branches <- 2 * n_species - 1 # number of branches in a rooted tree

# Initialize a vector for our moves
moves = VectorMoves()
\end{lstlisting}
\end{snugshade*}}
\item We specified a constant-rate birth-death process as our prior on the species tree. 
The birth-death process has a speciation and extinction rate as its parameters. 
We will use here a transformation and specify priors on the speciation rate and relative extinction rate.
Additionally, we calibrate the tree by assuming that the crown age of primates is around 75 MYA.
Thus, we specify a normal distribution with mean 75 and standard deviation 2.5 as the prior on the root age.
Since the root age can only be a positive real number we truncate the normal distribution at 0.
{\tt \begin{snugshade*}
\begin{lstlisting}
# Specify a prior on the diversification and turnover rate
speciation ~ dnGamma(2,2)
relativeExtinction ~ dnBeta(1,1)

# Now transform the diversification and turnover rates into speciation and extinction rates
extinction := speciation * relativeExtinction

# Specify a prior on the root age (our informed guess is about ~75 mya)
# Note that we use a truncated normal distribution because the root age must be positive
root ~ dnNormal(mean=75,sd=2.5,min=0.0, max=Inf)

sampling_fraction <- 23 / 450 # we sampled 23 out of the ~ 450 primate species

# create some moves that change the stochastic variables
# Moves are sliding and scaling proposals
moves.append( mvSlide(diversification,delta=1,tune=true,weight=2) )
moves.append( mvSlide(relativeExtinction,delta=1,tune=true,weight=2) )
moves.append( mvScale(diversification,lambda=1,tune=true,weight=2) )
moves.append( mvScale(relativeExtinction,lambda=1,tune=true,weight=2) )
moves.append( mvSlide(root,delta=1,tune=true,weight=0.2) )


# construct a variable for the tree drawn from a birth-death process
psi ~ dnBDP(lambda=speciation, mu=extinction, rootAge=root, rho=sampling_fraction, taxa=taxa )

moves.append( mvNarrow(psi, weight=5.0) )
moves.append( mvNNI(psi, weight=1.0) )
moves.append( mvFNPR(psi, weight=3.0) )
moves.append( mvGPR(psi, weight=3.0) )
moves.append( mvSubtreeScale(psi, weight=3.0) )
moves.append( mvNodeTimeSlideUniform(psi, weight=15.0) )


\end{lstlisting}
\end{snugshade*}}
\item Now that we have a species tree, which we assume is shared exactly for all genes.
That is, we assume each gene evolves under exactly the same tree, and thus each gene tree is equivalent to the species tree.
Nevertheless, we assume that each gene evolves at a different rate and with its own substitution model parameters.
Here we will assume for simplicity that every gene evolves under a global strict clock but has its own independent clock rate.
We assume that the logarithm of the clock rate is uniformly distribution, thus we specify in effect a log-uniform prior distribution.
This prior assumption means that we put the same prior probability on values of each magnitude, e.g., values between 0.0001 and 0.001 have the same prior probability as values between 10 and 100.
{\tt \begin{snugshade*}
\begin{lstlisting}
for ( i in 1:num_loci ) { 
   log_clock_rate[i] ~ dnUniform(-8,4)
   clock_rate[i] := 10^log_clock_rate[i]
   
   moves.append( mvSlide(log_clock_rate[i], weight=1.0) )
}
\end{lstlisting}
\end{snugshade*}}

\item Next we need our model for the substitution process. 
Hence, we just need to define the substitution matrix. 
We use a single HKY matrix that will apply to all sites per gene.
Additionally, we assume that sites evolve according to one of four possible rates, where each rate corresponds to a quantile from a gamma distribution.
{\tt \begin{snugshade*}
\begin{lstlisting}
for ( i in 1:num_loci ) {

    #### specify the HKY substitution model applied uniformly to all sites of a gene
    kappa[i] ~ dnLognormal(0,1)
    moves.append( mvScale(kappa[i],weight=1) )

    pi_prior[i] <- v(1,1,1,1) 
    pi[i] ~ dnDirichlet(pi_prior[i])
    moves.append( mvSimplexElementScale(pi[i],weight=2) )


    #### create a deterministic variable for the rate matrix
    Q[i] := fnHKY(kappa[i],pi[i]) 

    #### create the rates to model the gamma distributed rate variation among sites.
    alpha_prior[i] <- 0.05
    alpha[i] ~ dnExponential( alpha_prior[i] )
    gamma_rates[i] := fnDiscretizeGamma( alpha[i], alpha[i], 4, false )

    # add moves for the stationary frequencies, exchangeability rates and the shape parameter
    moves.append( mvScale(alpha[i],weight=2) )

}

\end{lstlisting}
\end{snugshade*}}


\item Finally, we can create our distribution for the character evolution.
We will use the common \cl{PhyloCTMC} distribution, which is a continuous time Markov process along a phylogenetic tree.
We create a \cl{seq} variable and attach/clamp each gene to one of the \cl{seq} variables.
{\tt \begin{snugshade*}
\begin{lstlisting}
for ( i in 1:num_loci ) { 
    # the sequence evolution model
    seq[i] ~ dnPhyloCTMC(tree=psi, Q=Q[i], branchRates=clock_rate[i], siteRates=gamma_rates[i], type="DNA")

    # attach the data
    seq[i].clamp(data[i])
}
\end{lstlisting}
\end{snugshade*}}


\item Now we have defined all the bricks of the model, and create our model object from it.
{\tt \begin{snugshade*}
\begin{lstlisting}
# We get a handle on our model.
# We can use any node of our model as a handle, here we choose to use the topology.
mymodel = model(psi)
\end{lstlisting}
\end{snugshade*}}


\item Finally, we need to perform inference under the model, using the data.
{\tt \begin{snugshade*}
\begin{lstlisting}
# Monitors to check the progression of the program
monitors[1] = mnScreen(printgen=100, root)
monitors[2] = mnModel(filename="output/primates_concatenation_root_calibration",printgen=10, separator = TAB)
monitors[3] = mnFile(filename="output/primates_concatenation_root_calibration",printgen=10, separator = TAB, psi)

# Here we use a plain MCMC. You could also set nruns=2 for a replicated analysis
# or use mcmcmc with heated chains.
mymcmc = mcmc(mymodel, monitors, moves)

# This should be sufficient to obtain enough MCMC samples
mymcmc.burnin(generations=3000,tuningInterval=100)
mymcmc.run(generations=10000)
\end{lstlisting}
\end{snugshade*}}

\item Now we can perform some post-run analyses.
{\tt \begin{snugshade*}
\begin{lstlisting}
# Now, we will analyze the tree output.
# Let us start by reading in the tree trace
treetrace = readTreeTrace("output/primates_concatenation_root_calibration", treetype="clock")
# and get the summary of the tree trace
treetrace.summarize()

mapTree(treetrace,"output/primates_concatenation_root_calibration")
\end{lstlisting}
\end{snugshade*}}

%\end{itemize}
\end{enumerate}




\bibliographystyle{sysbio}
\bibliography{\GlobalResourcePath refs}

